buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

repositories {
    maven { url 'https://kotlin.bintray.com/kotlinx' }
    maven { url 'https://dl.bintray.com/kotlin/ktor' }

    google()
    jcenter()
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-platform-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion android_compile_sdk_version as Integer
    defaultConfig {
        applicationId "io.ktor.samples.mpp.client"
        minSdkVersion android_min_sdk_version as Integer
        targetSdkVersion android_target_sdk_version as Integer
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    expectedBy project(':client-mpp')

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'com.android.support:appcompat-v7:28.0.0-rc02'
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinx_coroutines_version"
    implementation "io.ktor:ktor-client-android:$ktor_version"

    testImplementation "junit:junit:$junit_version"
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

def androidHome = project.hasProperty("sdk.dir") ?: System.getenv("ANDROID_HOME")
def androidTools = "${androidHome}/tools"
def androidPlatformTools = "${androidHome}/platform-tools"
def adb = "${androidPlatformTools}/adb"

task emulatorList(type: Exec) {
    workingDir "."
    executable "sh"
    args "-c", "${androidTools}/emulator -list-avds"
}

task emulatorStart(type: Exec) {
    // @TODO: Execute in the background!
    workingDir "."
    executable "sh"
    args "-c", "${androidTools}/emulator -avd Android_Accelerated_Nougat"
}

task emulatorInstall(type: Exec, dependsOn: [build]) {
    workingDir "."
    executable "sh"
    args "-c", "${adb} install -r ${file('build/outputs/apk/debug/android-debug.apk')}"
}

task emulatorRun(type: Exec, dependsOn: [emulatorInstall]) {
    workingDir "."
    executable "sh"
    args "-c", "${adb} shell am start -a android.intent.action.MAIN -n io.ktor.samples.mpp.client/.MainActivity"

}

kotlin.experimental.coroutines 'enable'
